import java.util.regex.Matcher
import java.util.regex.Pattern
import org.yaml.snakeyaml.Yaml

File pubspec = new File(project.projectDir.parentFile, 'pubspec.yaml')
String yaml = pubspec.text
// Using \s*['|"]?([^\n|'|"]*)['|"]? to extract version number.
Matcher versionMatcher = Pattern.compile("^version:\\s*['|\"]?([^\\n|'\"]*)['|\"]?\$", Pattern.MULTILINE).matcher(yaml)
versionMatcher.find()
String library_version = versionMatcher.group(1).replaceAll("\\+", "-")
File flutter_project_dir = rootProject.projectDir.parentFile
def cfg = new Yaml().load(new File(flutter_project_dir, 'pubspec.yaml').text)
String app_id = cfg.vivo_push?.app_id
String app_key = cfg.vivo_push?.app_key
if (null == app_id || null == app_key) {
    throw new IllegalArgumentException("app_id or app_key is null, add code in pubspec.yaml:\nvivo_push:" +
            "\n  app_id: \${your vivo app id}" +
            "\n  app_key: \${your vivo app key}")
}
group = "com.skyza.vivo_push"
version = library_version

buildscript {
    ext.kotlin_version = "2.1.0"
    repositories {
        google()
        mavenCentral()

        maven {
            url 'https://repos.vivo.com.cn/maven/repository/external-lib/'
            allowInsecureProtocol = true
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath 'org.yaml:snakeyaml:2.5'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()

        maven {
            url 'https://repos.vivo.com.cn/maven/repository/external-lib/'
            allowInsecureProtocol = true
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "com.skyza.vivo_push"

    compileSdk = 35

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 23
        manifestPlaceholders += [
                APP_ID : app_id,
                APP_KEY: app_key,
        ]
        consumerProguardFiles 'consumer-rules.pro'
    }

    dependencies {
        implementation("vivo:vpush:4.1.0.0")
        testImplementation("org.jetbrains.kotlin:kotlin-test")
        testImplementation("org.mockito:mockito-core:5.0.0")
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}

